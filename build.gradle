/*
 *    Copyright 2025 iceBear67 and Contributors
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 *
 */

plugins {
    id 'java'
    id 'jacoco'
    id("io.freefair.lombok") version libs.versions.lombok
}

allprojects {
    group = property("group")
    version = property("version")
    if (this != project) {
        configureModules(project)
    }
}

def configureModules(Project project) {
    project.with {
        apply plugin: 'java'
        apply plugin: 'java-library'
        apply plugin: 'io.freefair.lombok'
        repositories {
            mavenLocal() // for testing
            mavenCentral()
            maven {
                name "kalculosSnapshots"
                url "https://repo.sfclub.cc/snapshots"
            }
        }
        configureJava(project)
    }
}

def configureJava(Project project){
    project.with {
        def targetJavaVersion = Integer.parseInt(getProperty("targetJava") as String)
        java {
            def javaVersion = JavaVersion.toVersion(targetJavaVersion)
            sourceCompatibility = javaVersion
            targetCompatibility = javaVersion
            if (JavaVersion.current() < javaVersion) {
                toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
            }
        }
        tasks.withType(JavaCompile).configureEach {
            if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
                options.release = targetJavaVersion
            }
        }
        test {
            useJUnitPlatform()
        }

        dependencies {
            testImplementation platform('org.junit:junit-bom:5.13.4')
            testImplementation 'org.junit.jupiter:junit-jupiter'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        }
    }

}