/*
 *    Copyright 2025 iceBear67 and Contributors
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 *
 */

plugins {
    id 'application'
}

jar {
    manifest {
        attributes(Map.of(
                "Premain-Class", "io.ib67.edge.EdgeTransformerAgent"
        ))
    }
    application.applicationDistribution.with {
        it.from(archiveFile) {
            into("agent/")
        }
        into("doc/") {
            from("${rootProject.layout.projectDirectory}/LICENSE")
        }
    }

}

application {
    mainClass = "io.ib67.edge.Edge"
    applicationName = "edged"
    applicationDefaultJvmArgs = ["-javaagent:../agent/${jar.archiveFileName.get()}"]
}

run {
    standardInput = System.in
}

dependencies {
    api libs.asm
    runtimeOnly project(":server")
    testImplementation project(":server")
}
tasks {
    def agentPath = "${jar.archiveFile.get()}"
    test {
        dependsOn(jar)
        jvmArgs += "-javaagent:${agentPath}"
    }
    run {
        dependsOn(tasks.jar)
        file("run").mkdir()
        workingDir(file("run"))
        jvmArgs("-javaagent:${agentPath}")
    }
}